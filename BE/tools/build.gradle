plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.0' apply false
	id 'io.spring.dependency-management' version '1.1.7' apply false
}

group = 'com.ttl'
version = '0.0.1-SNAPSHOT'

ext['graphql-java.version'] = '20.0'


subprojects {
	apply plugin: 'java-library'
	apply plugin: 'io.spring.dependency-management'

	group = 'com.ttl.tool'
	version = '0.0.1-SNAPSHOT'

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(17)
		}
	}

	repositories {
		mavenCentral()
	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.boot:spring-boot-dependencies:4.0.0"
		}
	}

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
		all {
			resolutionStrategy {
				force 'com.graphql-java:graphql-java:20.0'
				force 'com.graphql-java:graphql-java-extended-scalars:20.0'
			}
		}
	}

	dependencies {
		// Lombok for all modules
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'

		// Testing
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	}

	tasks.named('test') {
		useJUnitPlatform()
	}
}
